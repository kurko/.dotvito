" Copy or symlink to ~/.vimrc or ~/_vimrc.

call pathogen#infect()

" avoiding annoying CSApprox warning message
let g:CSApprox_verbose_level = 0
set nocompatible                  " Must come first because it changes other options.

set tags=tags                     " ctags

syntax on                         " Turn on syntax highlighting.
filetype plugin indent on         " Turn on file type detection.

runtime macros/matchit.vim        " Load the matchit plugin.

set showcmd                       " Display incomplete commands.
set showmode                      " Display the mode you're in.

set backspace=indent,eol,start    " Intuitive backspacing.

set hidden                        " Handle multiple buffers better.

set wildmenu                      " Enhanced command line completion.
set wildmode=list:longest         " Complete files like a shell.

set ignorecase                    " Case-insensitive searching.
set smartcase                     " But case-sensitive if expression contains a capital letter.

set number                        " Show line numbers.
set ruler                         " Show cursor position.

set incsearch                     " Highlight matches as you type.
set hlsearch                      " Highlight matches.

set wrap                          " Turn on line wrapping.
set scrolloff=3                   " Show 3 lines of context around the cursor.

set title                         " Set the terminal's title

set visualbell                    " No beeping.

set nobackup                      " Don't make a backup before overwriting a file.
set nowritebackup                 " And again.
set directory=/var/tmp            " Keep swap files in one location

" UNCOMMENT TO USE
set tabstop=2                    " Global tab width.
set shiftwidth=2                 " And again, related.
set expandtab                    " Use spaces instead of tabs

set laststatus=2                  " Show the status line all the time
" Useful status information at bottom of screen
set statusline=[%n]\ %<%.99f\ %h%w%m%r%y\ %{exists('*CapsLockStatusline')?CapsLockStatusline():''}%=%-16(\ %l,%c-%v\ %)%P

set foldmethod=syntax
set foldlevelstart=99
set foldlevel=99
au BufRead,BufNewFile *.scss set filetype=scss
au BufRead,BufNewFile *.ejs set filetype=html

" "Theme
colorscheme ir_black
"railscasts


""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" DEFAULT BEHAVIORS
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" PLUGINS CONFIGURATIONS
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" git gutter plugin
let g:gitgutter_enabled = 1

" zencoding plugin
 let g:user_zen_settings = {
  \  'php' : {
  \    'extends' : 'html',
  \    'filters' : 'c',
  \  },
  \  'xml' : {
  \    'extends' : 'html',
  \  },
  \  'erb' : {
  \    'extends' : 'html',
  \  },
  \  'haml' : {
  \    'extends' : 'html',
  \  },
  \}

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" CtrlP
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set runtimepath^=~/.vim/bundle/ctrlp.vim
let g:ctrlp_custom_ignore = 'tmp\|log\|public\|vendor/assets\|vendor/bundle\|vendor/plugins\|spec/cassett\|\.git\|cassett\|/.git\|/app/assets/image\|/coverage'

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" CUSTOM SHORTCUTS & KEY BINDINGS
"
" For tests & specs custom functions and key bindings, look at the respective
" section below
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

  set timeoutlen=200
  let mapleader = ","

  " save by pressing ,s instead of :w
  map <Leader>s <ESC>:w<CR>
  map <Leader>be :BufExplorer<CR>

  nnoremap <leader>1 :set foldlevel=1<cr>
  nnoremap <leader>2 :set foldlevel=2<cr>
  nnoremap <leader>3 :set foldlevel=3<cr>
  nnoremap <leader>4 :set foldlevel=4<cr>

  " Tab mappings.
    map <leader>tt :tabnew<cr>
    map <leader>te :tabedit
    map <leader>tc :tabclose<cr>
    map <leader>to :tabonly<cr>
    map <leader>tn :tabnext<cr>
    map <leader>tp :tabprevious<cr>
    map <leader>tf :tabfirst<cr>
    map <leader>tl :tablast<cr>
    map <leader>tm :tabmove

  " Enter clears search highlight
  nmap <CR> :nohlsearch<CR>

  " Goes to the end of the line (pressing shift+4 is annoying)
  nmap 44 $
  vmap 44 $h

  " Tab becomes Esc
  " inoremap <Tab> <Esc>

  " Updates CTags
  map <F5> :exec "!ctags -R --exclude=.git --exclude=vendor/bundle"<CR>

  " Resizing windows
  nnoremap <C-h> :vertical resize +5<cr>
  nnoremap <C-l> :vertical resize -5<cr>
  nnoremap <C-k> :resize +5<cr>
  nnoremap <C-j> :resize -5<cr>

  inoremap  <Up>     <NOP>
  inoremap  <Down>   <NOP>
  inoremap  <Left>   <NOP>
  inoremap  <Right>  <NOP>
  noremap   <Up>     <NOP>
  noremap   <Down>   <NOP>
  noremap   <Left>   <NOP>
  noremap   <Right>  <NOP>

  " When you've writen : (colon), press %% very fast and it'll write the current
  " file's directory
  cnoremap %% <C-R>=expand('%:h').'/'<cr>
  map <leader>e :edit %%
  map <leader>v :view %%

  " RUBY & RAILS
    " Keeps current file in on side, and its test file in the other
    " (depends on Rails.vim)
    map <leader>A :on<CR>\|:vsplit<CR>\|<c-w>l\|:A<CR>
    " adds <%= %> in the current line
    inoremap <leader>e <%=  %><ESC>==lllli
    nnoremap <leader>e i<%=  %><ESC>==lllli

    " :A is from rails.vim plugin
    nnoremap <Leader>a :A<CR>

    " deletes the remaining chars from a string
    nnoremap <Leader>ds vf"hd

  " CTRLP
    map <leader>ga :CtrlPClearCache<cr>\|:CtrlP app/assets<cr>
    map <leader>gv :CtrlPClearCache<cr>\|:CtrlP app/views<cr>
    map <leader>gc :CtrlPClearCache<cr>\|:CtrlP app/controllers<cr>
    map <leader>gm :CtrlPClearCache<cr>\|:CtrlP app/models<cr>
    map <leader>gl :CtrlPClearCache<cr>\|:CtrlP lib<cr>
    map <leader>gs :CtrlPClearCache<cr>\|:CtrlP spec<cr>
    map <leader>gu :CtrlPClearCache<cr>\|:CtrlP spec/unit<cr>
    map <leader>f :CtrlPClearCache<cr>\|:CtrlP<cr>
    map <leader>f :CtrlPClearCache<cr>\|:CtrlP<cr>
    map <leader>F :CtrlPClearCache<cr>\|:CtrlP %%<cr>


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" CUSTOM COMMANDS
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
function! SetCtrlPPathTheSameAsVim()
  let g:ctrlp_working_path_mode=''
endfunction

" When you go to another project inside of a Rails project (e.g Ember project
" in app/assets/javascript/my_project), CtrlP still uses ./ as root. By typing
" this command, CtrlP will :cd into this internal project.
command! CtrlPVimPath call SetCtrlPPathTheSameAsVim()

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" CUSTOM FUNCTIONS
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

  " Rename current file
  function! RenameFile()
    let old_name = expand('%')
    let new_name = input('Renaming file. New file name: ', expand('%'), 'file')
    if new_name != '' && new_name != old_name
      exec ':saveas ' . new_name
      exec ':silent !rm ' . old_name
      redraw!
    endif
  endfunction

  " Copy current file to a new location
  function! CopyFile()
    let old_name = expand('%')
    let new_name = input('Copying file. New file name: ', expand('%'), 'file')
    if new_name != '' && new_name != old_name
      exec ':saveas ' . new_name
      redraw!
    endif
  endfunction

  map <leader>n :call RenameFile()<cr>
  map <leader>c :call CopyFile()<cr>

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" TESTS & SPECS FUNCTIONS
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
  map <leader>t :call RunTestFile()<cr>
  map <leader>r :call RunNearestTest()<cr>
  map <leader>bt <ESC>:w<CR>\|:!bundle exec rspec --color --no-drb %<cr>
  map <leader>z <ESC>:w<CR>\|:!zeus rspec --color --no-drb %<cr>
  map <leader>tu <ESC>:w<CR>\|:!rspec --color --no-drb spec/lib<cr>
  map <leader>bu <ESC>:w<CR>\|:!bundle exec rspec --color --no-drb spec/lib<cr>

  function! RunTestFile(...)
    if a:0
      let command_suffix = a:1
    else
      let command_suffix = ""
    endif

    " Run the tests for the previously-marked file.
    let in_test_file = match(expand("%"), '\(.feature\|_spec.rb\|_test.rb\|_test.js\|_spec.js\)')

    if in_test_file >= 0
      call SetTestFile(command_suffix)
    elseif !exists("t:grb_test_file")
      :echo "Vim: I don't know what file to test :("
      return
    end

    call RunTests(t:grb_test_file . t:grb_test_line)
  endfunction

  function! RunNearestTest()
    let spec_line_number = line('.')
    call RunTestFile(":" . spec_line_number)
  endfunction

  function! SetTestFile(...)
    " Set the spec file that tests will be run for.
    if a:0 && a:1 != ""
      let t:grb_test_line = a:1
    else
      let t:grb_test_line = ""
    endif

    let t:grb_test_file = @%
  endfunction

  function! RunTests(filename)

    " Write the file and run tests for the given filename
    :w
    :silent !echo;echo;echo;echo;echo;echo;echo;echo;echo;echo
    :silent !echo;echo;echo;echo;echo;echo;echo;echo;echo;echo
    :silent !echo;echo;echo;echo;echo;echo;echo;echo;echo;echo
    :silent !echo;echo;echo;echo;echo;echo;echo;echo;echo;echo
    :silent !echo;echo;echo;echo;echo;echo;echo;echo;echo;echo
    :silent !echo;echo;echo;echo;echo;echo;echo;echo;echo;echo
    :silent !echo;echo;echo;echo;echo;echo;echo;echo;echo;echo
    :silent !echo;echo;echo;echo;echo;echo;echo;echo;echo;echo
    :silent !echo;echo;echo;echo;echo;echo;echo;echo;echo;echo

    " JAVASCRIPT
    if match(expand("%"), '\(._test.js\|_spec.js\)') >= 0

      let filename_for_spec = substitute(expand("%"), "spec/javascripts/", "", "")
      "Konacha
      if filereadable("Gemfile") && match(readfile("Gemfile"), "konacha") >= 0

        " Konacha with Zeus
        if filereadable("zeus.json")
          :silent !echo "Konacha with zeus"
          exec ":!zeus rake konacha:run SPEC=" . filename_for_spec

        " Konacha with bundle exec
        else
          :silent !echo "Konacha with bundle exec"
          exec ":!bundle exec rake konacha:run SPEC=" . filename_for_spec
        endif

      " Everything else (QUnit)
      else
        "Rake
        :silent !echo "Javascript test, running rake"
        exec ":!rake"
      endif

    " RUBY
    elseif match(a:filename, '\(._test.rb\|_spec.rb\)') >= 0

      let filename_without_line_number = substitute(a:filename, ':\d\+$', '', '')
      " Minitest
      if match(a:filename, '\(_test\)') != -1
        exec ":!ruby -Ilib/ " . a:filename

      " Bundler
      elseif match(readfile(filename_without_line_number), '\("spec_helper\|''spec_helper\|capybara_helper\|acceptance_spec_helper\|acceptance_helper\)') >= 0

        " Spring (gem like Zeus, to make things faster)
        if match(system('spring status'), 'Spring is running') >= 0
          :silent !echo "Using Spring"
          exec ":!spring rspec -O ~/.rspec --color --format progress --no-drb --order random " . a:filename

        " Zeus
        elseif filereadable("zeus.json") && filereadable("Gemfile")
          :silent !echo "Using zeus"
          exec ":!zeus rspec -O ~/.rspec --color --format progress --no-drb --order random " . a:filename

        " bundle exec
        elseif filereadable("Gemfile")
          :silent !echo "Using bundle exec"
          exec ":!bundle exec rspec --color --order random " . a:filename

        " pure rspec
        else
          :silent !echo "Using vanilla rspec"
          exec ":!rspec -O ~/.rspec --color --format progress --no-drb --order random " . a:filename
        end

      " Everything else
      else
        :silent !echo "Using vanilla rspec outside Rails"
        exec ":!rspec -O ~/.rspec --color --format progress --no-drb --order random " . a:filename
      end
    end
  endfunction

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" ENVIRONMENT CONFIGURATIONS
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
  " taken from http://vim.wikia.com/wiki/Highlight_unwanted_spaces
  highlight RedundantSpaces ctermbg=red guibg=red
  match RedundantSpaces /\s\+$/
  autocmd BufWinEnter * match RedundantSpaces /\s\+$/
  autocmd InsertEnter * match RedundantSpaces /\s\+\%#\@<!$/
  autocmd InsertLeave * match RedundantSpaces /\s\+$/
  autocmd BufWinLeave * call clearmatches()

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" MULTIPURPOSE TAB KEY
" Indent if we're at the beginning of a line. Else, do completion.
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
function! InsertTabWrapper()
    let col = col('.') - 1
    if !col || getline('.')[col - 1] !~ '\k'
        return "\<tab>"
    else
        return "\<c-p>"
    endif
endfunction
inoremap <tab> <c-r>=InsertTabWrapper()<cr>
inoremap <s-tab> <c-n>

"highlight ColorColumn ctermbg=7
set colorcolumn=80
